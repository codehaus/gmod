apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

// Set Maven coordinates
group = 'org.codehaus.groovy.modules.groovyfx'
archivesBaseName = "groovyfx"
version = '0.1-SNAPSHOT'

// Read environment and set properties
javafxHome = System.env['JAVAFX_HOME']
isRelease = !version.endsWith('SNAPSHOT')

// Define a source set for the non-standard src/demo directory
sourceSets {
    demo {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        groovy {
            srcDir = 'src/demo/groovy'
        }
        resources {
            srcDir = 'src/demo/resources'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:1.8.0'
    compile 'asm:asm:3.2'
    compile files("${javafxHome}/rt/lib/jfxrt.jar")
}

// Add a dynamic task to run each of the demos in src/demo/groovy
new File('src/demo/groovy').eachFileMatch(~/.*Demo.groovy/) { file ->
    def taskName = file.name - '.groovy'
    task "$taskName"(type: JavaExec) {
        group = "Demo"
        description = "Run the $taskName program"
        main = "demo.$taskName"
        classpath sourceSets.main.runtimeClasspath + sourceSets.demo.runtimeClasspath
    }
}

