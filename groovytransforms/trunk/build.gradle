apply plugin: 'groovy'
apply plugin: 'scala'
apply plugin: 'maven'

version = '0.2'
group = 'org.codehaus.groovy.modules'

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    mavenRepo name: 'ScalaTools', urls: 'http://www.scala-tools.org/repo-releases'
}

dependencies {
    groovy 'org.codehaus.groovy:groovy:1.7.10'
    scalaTools 'org.scala-lang:scala-compiler:2.8.1'
    scalaTools 'org.scala-lang:scala-library:2.8.1'

    compile 'org.scala-lang:scala-library:2.8.1'
    testCompile('org.scalatest:scalatest:1.3'){ transitive = false }
    testCompile 'junit:junit:4.8.2'

    deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2',
                 'org.apache.maven:maven-ant-tasks:2.1.0'
}

artifacts {
    archives jar
}

installer = install.repositories.mavenInstaller
def deployer = null

if(!project.hasProperty('codehausUsername')) codehausUsername = ''
if(!project.hasProperty('codehausPassword')) codehausPassword = ''

uploadArchives {
    repositories {
        deployer = mavenDeployer {
            name = 'codehausDeployer'
            configuration = configurations.deployerJars
            uploadDescriptor = true
            repository(url: 'https://dav.codehaus.org/repository/gmod/') {
                authentication(userName: codehausUsername, password: codehausPassword)
            }
            snapshotRepository(url: 'https://dav.codehaus.org/snapshots.repository/gmod/') {
                uniqueVersion = false
                authentication(userName: codehausUsername, password: codehausPassword)
            }
        }
    }
}

[installer, deployer].each { repo ->
    configure(repo) {
        pom.project {
            name 'Groovy Transforms'
            description 'Additional AST Transformations for the Groovy language'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            repositories {
                repository {
                    id('codehaus-release')
                    name('Codehaus')
                    url('http://repository.codehaus.org')
                }
            }
            issueManagement {
                system 'JIRA'
                url 'http://jira.codehaus.org/browse/GMOD'
            }
            scm {
                developerConnection 'scm:https://svn.codehaus.org/gmod/groovytransforms/'
                connection 'scm:http://svn.codehaus.org/gmod/groovytransforms/'
                tag 'trunk'
                url 'http://fisheye.codehaus.org/browse/gmod/groovytransforms/'
            }
            developers {
                developer {
                    name 'Andres Almiray'
                    id 'aalmiray'
                }
            }
            /*distributionManagement {
                repository {
                    id('codehaus.org')
                    name('Griffon Central Repository')
                    url('dav:https://dav.codehaus.org/repository/gmod/')
                }
                snapshotRepository {
                    uniqueVersion('false')
                    id('codehaus.org')
                    name('Griffon Central Development Repository')
                    url('dav:https://dav.codehaus.org/snapshots.repository/gmod/')
                }
            }*/
        }
    }
}
